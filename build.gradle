apply plugin: 'java'
apply plugin: 'antlr'
apply plugin: 'maven-publish'

dependencies {
	antlr 'org.antlr:antlr:3.5'

	compile 'commons-lang:commons-lang:2.6'
	compile 'commons-collections:commons-collections:3.2.1'
	compile 'com.googlecode.json-simple:json-simple:1.1'
	runtime 'com.oracle:ojdbc6:11.2.0.4.0'

	testCompile 'junit:junit:4.10'
}

test {
	testLogging {
		//events "passed", "skipped", "failed", "standardOut", "standardError"
		afterSuite { desc, result ->
			if (!desc.parent) { // will match the outermost suite
				println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
			}
		}
	}
}

task sourceJar(type: Jar) {
	from sourceSets.main.allJava
}

publishing {
	repositories {
		maven {
			url llNexusRepoUrl
			
			credentials {
				username = llNexusRepoUsername
				password = llNexusRepoPassword
			}
		}
	}

	publications {
		mavenJava(MavenPublication) {
			groupId 'com.livinglogic'
			artifactId 'ul4'
			version version
			
			pom.withXml {
				def root = asNode()
				root.appendNode('description', 'Java implementation of the UL4 templating language')
				root.appendNode('url', 'http://www.livinglogic.de/Python/ul4c/')
				root.appendNode('inceptionYear', '2008')
				
				def organization = root.appendNode('organization')
				organization.appendNode('name', 'LivingLogic AG')
				organization.appendNode('url', 'http://www.livinglogic.de')
				
				def developers = root.appendNode('developers')
				def developer = developers.appendNode('developer')
				developer.appendNode('name', 'Walter Dörwald')
				developer.appendNode('email', 'walter@livinglogic.de')
				developer.appendNode('timezone', 'Europe/Munich')
				developer = developers.appendNode('developer')
				developer.appendNode('name', 'Andreas Gaßner')
				developer.appendNode('email', 'ag@livinglogic.de')
				developer.appendNode('timezone', 'Europe/Munich')
				
				def scm = root.appendNode('scm')
				scm.appendNode('url', 'https://github.com/LivingLogic/LivingLogic.Java.ul4')
			}
			
			from components.java

			artifact sourceJar {
				classifier "sources"
			}
		}
	}
}
